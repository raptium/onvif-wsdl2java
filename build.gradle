plugins {
    id "de.undercouch.download" version "5.1.0"
    id "com.yupzip.wsdl2java" version "2.3.1"
    id "maven-publish"
}

repositories {
    mavenCentral()
}

version = "1.0.0-SNAPSHOT"

wsdl2java {
    generatedWsdlDir = file("$projectDir/build/generated-sources/wsdl2java")
    wsdlDir = file("$projectDir/src/main/resources/onvif")
    wsdlsToGenerate = []

    def files = fileTree("src/main/resources/onvif").include("**/*.wsdl")
    files.each {
        wsdlsToGenerate << ["-server", "-client", "-catalog", "$projectDir/src/main/resources/xml-catalog.xml", "-autoNameResolution", "-verbose", it]
    }
}

wsdl2javaTask.dependsOn("downloadWsdls")

sourceSets.main.java.srcDir("$projectDir/build/generated-sources/wsdl2java")

ext {
    wsdls = [
            "$projectDir/src/main/resources/onvif"     : [
                    "https://www.onvif.org/ver10/actionengine.wsdl",
                    "https://www.onvif.org/ver10/advancedsecurity/wsdl/advancedsecurity.wsdl",
                    "https://www.onvif.org/ver10/analyticsdevice.wsdl",
                    "https://www.onvif.org/ver10/credential/wsdl/credential.wsdl",
                    "https://www.onvif.org/ver10/device/wsdl/devicemgmt.wsdl",
                    "https://www.onvif.org/ver10/deviceio.wsdl",
                    "https://www.onvif.org/ver10/display.wsdl",
                    "https://www.onvif.org/ver10/events/wsdl/event.wsdl",
                    "https://www.onvif.org/ver10/media/wsdl/media.wsdl",
                    "https://www.onvif.org/ver10/pacs/accesscontrol.wsdl",
                    "https://www.onvif.org/ver10/pacs/accessrules.wsdl",
                    "https://www.onvif.org/ver10/pacs/doorcontrol.wsdl",
                    "https://www.onvif.org/ver10/pacs/types.xsd",
                    "https://www.onvif.org/ver10/provisioning/wsdl/provisioning.wsdl",
                    "https://www.onvif.org/ver10/receiver.wsdl",
                    "https://www.onvif.org/ver10/recording.wsdl",
                    "https://www.onvif.org/ver10/replay.wsdl",
                    "https://www.onvif.org/ver10/schedule/wsdl/schedule.wsdl",
                    "https://www.onvif.org/ver10/schema/common.xsd",
                    "https://www.onvif.org/ver10/schema/metadatastream.xsd",
                    "https://www.onvif.org/ver10/schema/onvif.xsd",
                    "https://www.onvif.org/ver10/search.wsdl",
                    "https://www.onvif.org/ver10/thermal/wsdl/thermal.wsdl",
                    "https://www.onvif.org/ver20/analytics/radiometry.xsd",
                    "https://www.onvif.org/ver20/analytics/wsdl/analytics.wsdl",
                    "https://www.onvif.org/ver20/analytics/rules.xsd",
                    "https://www.onvif.org/ver20/analytics/humanface.xsd",
                    "https://www.onvif.org/ver20/analytics/humanbody.xsd",
                    "https://www.onvif.org/ver20/imaging/wsdl/imaging.wsdl",
                    "https://www.onvif.org/ver20/media/wsdl/media.wsdl",
                    "https://www.onvif.org/ver20/ptz/wsdl/ptz.wsdl"
            ],
            "$projectDir/src/main/resources/oasis-open": [
                    "http://docs.oasis-open.org/wsn/b-2.xsd",
                    "http://docs.oasis-open.org/wsn/bw-2.wsdl",
                    "http://docs.oasis-open.org/wsn/t-1.xsd",
                    "http://docs.oasis-open.org/wsrf/bf-2.xsd",
                    "http://docs.oasis-open.org/wsrf/r-2.xsd",
                    "http://docs.oasis-open.org/wsrf/rw-2.wsdl"
            ],
            "$projectDir/src/main/resources/w3"        : [
                    "https://www.w3.org/2001/xml.xsd",
                    "https://www.w3.org/2003/05/soap-envelope",
                    "https://www.w3.org/2004/08/xop/include",
                    "https://www.w3.org/2005/05/xmlmime",
                    "https://www.w3.org/2005/08/addressing/ws-addr.xsd"
            ]
    ]
    patches = [
            "$projectDir/src/main/resources/onvif/ver10/pacs/accessrules.wsdl": "$projectDir/src/main/patches/accessrules.patch"
    ]
}

task downloadWsdls {
    doLast {
        wsdls.each { destinationDirectory, sourceFiles ->
            sourceFiles.each { sourceFile ->
                def destinationFile = new File(destinationDirectory, new URL(sourceFile).path)
                if (!destinationFile.exists()) {
                    download.run {
                        src sourceFile
                        dest destinationFile.absolutePath
                    }
                }
            }
        }
        patches.each { origin, patch ->
            ant.patch(patchfile: patch, originalFile: origin)
        }
    }
}

task deleteFiles(type: Delete) {
    delete wsdl2java.generatedWsdlDir, wsdls.keySet()
}

clean.dependsOn deleteFiles

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/raptium/onvif-wsdl2java"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "net.raptium"
            artifactId = "onvif-wsdl2java"
            version = "${project.version}"
            from components.java
        }
    }
}

sourcesJar {
    dependsOn wsdl2javaTask
}
